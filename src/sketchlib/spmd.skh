#ifndef _SPMD_H
#define _SPMD_H

// need to define SPMD_MAX_BUF_LEN and SPMD_MAX_NPROC before including!

struct spmdinfo {
  float [SPMD_MAX_BUF_LEN * SPMD_MAX_NPROC] buf;
  bit [SPMD_MAX_NPROC] pendingData;
}

@Native("{ _out = NULL; }")
global spmdinfo newspmdinfo(global int nproc, global int maxlen)
{
    assert nproc <= SPMD_MAX_NPROC;
    assert maxlen <= SPMD_MAX_BUF_LEN;
    global spmdinfo info = new spmdinfo();
    return info;
}


@Native("{ mpiTransfer(size, src_buff, scond, recipient, dest_buff, rcond); }" )
void spmdtransfer(global spmdinfo info, global int size, float [size] src_buff, bit scond, int recipient, ref float [size] dest_buff, bit rcond)
{
  spmdbarrier();
  assert recipient>=0 && recipient<spmdnproc;
  assert size>0 && size<=SPMD_MAX_BUF_LEN;
  info.pendingData[spmdpid] = 0;
  spmdbarrier();
  for (int i=0; i<size; ++i) {
    info.buf[recipient*SPMD_MAX_BUF_LEN + i] = src_buff[i];
  }
  if (scond) {
    assert !info.pendingData[recipient] : "two senders trying to send to the same recipient!";
    info.pendingData[recipient] = 1;
  }
  spmdbarrier();
  if (rcond) {
    assert info.pendingData[spmdpid] : "trying to receive but no one sent!";
    for (int i=0; i<size; ++i) {
      dest_buff[i] = info.buf[spmdpid*SPMD_MAX_BUF_LEN + i];
    }
  } else {
    assert !info.pendingData[spmdpid] : "pending data but not receiving!";
  }
}

#endif  //_SPMD_H
