int N;
int[N] test16(int[N] in){
  int[N] out;
  for(int i=0; i<N; ++i){
  	if(i>0 && i<N-1) out[i] = in[i-1] + in[i+1];
  	else out[i] = 0;
  }
  return out;
}

generator int exp(){ /* automatically rewritten */
	int rv = 0;
	if(??){ rv = rv + ??;} else { rv = rv - ??; } 	
	if(??){ rv = rv + N; }	
	return rv;
}

int[N] testSK16(int[N] in) implements test16 {
  int[N] out = 0;
  if( N>= 4){
	  out[exp()] = in[exp()];
	  out[exp()] = in[exp()];
	  for(int i=??; i<N-??; ++i){  	
	  	out[i-??] += in[i];
	  	out[i+??] = in[i];
	  }
	  out[exp()] += in[exp()];
	  out[exp()] += in[exp()];
  }else{
	  for(int i=??; i<N-??; ++i){
	  	 out[i] = in[i-??] + in[i+??];
	  }
  }
  return out;
}
