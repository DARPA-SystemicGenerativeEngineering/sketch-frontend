include "options.skh";

int[N] sort(int[N] input){
   int[N] output=input;
   int[N] done = 0;
   int k=0;
   for(int i=0; i<N; ++i){
      for(int j=i+1; j<N; ++j){
         if( output[j]< output[i]){
            int tmp = output[j];
            output[j] = output[i];
            output[i] = tmp;
         }
      }
   }
   return output;
}
generator int abs(int x){
	if(x<0) return -x;
	else return x;
}
generator int min(int x,int y){
	if(x < y) return x;
	else return y;
}
harness void main(int[N] arr){//find closest pair distance in arr
	if(N<2) return;
	int currmin = abs(arr[1] - arr[0]);
	for(int i=0;i<N;++i){
		for(int j=i+1;j<N;++j){
			currmin = min(abs(arr[j]-arr[i]),currmin);
		}
	}
	
	int[N] sarr = sort(arr);

	int currmin2 = sarr[??]-sarr[??];
	for(int i=??;{|i  (< | <= | > | >=) (??*N (+ | -) ??) |};i={| i (+|-) ??|}){
		currmin2 = min(sarr[{| i (+|-) ??|}] - sarr[i],currmin2);
	}
	assert(currmin == currmin2);
}
