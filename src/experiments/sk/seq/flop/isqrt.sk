#include "test/flop/floatAdd.skh"
#include "test/flop/floatMul.skh"

bit[FW] FObitsub(bit[FW] a, bit[FW] b)
{
	for(int i=0;i<FW;i++)
		b[i]=!b[i];
	bit[FW] one=0;
	one[0]=1;
	b=b+a;
	return b+one;
}

bit[FW] floatDiv2(bit[FW] a)
{
	bit[E] e=a[M::E];
	bit[E] n_one=0;
	for(int i=0;i<E;i++)
		n_one[i]=1;
	a[M::E]=e+n_one;
	return a;
}

bit[FW] floatDiv4(bit[FW] a)
{
	bit[E] e=a[M::E];
	bit[E] n_two=0;
	for(int i=0;i<E-1;i++)
		n_two[i]=1;
	a[M::E]=e+n_two;
	return a;
}

bit[FW] floatDiv16(bit[FW] a)
{
	bit[E] e=a[M::E];
	bit[E] n_four=0;
	for(int i=0;i<E-2;i++)
		n_four[i]=1;
	a[M::E]=e+n_four;
	return a;
}

bit[FW] floatAbs(bit[FW] a)
{
	a[SB]=0;
	return a;
}

bit[FW] isqrt(bit[FW] in)
{
	bit[FW] t=??;
	t=FObitsub(t,(in>>1));
	t[M-1]=1;
	t[SB]=0;

	bit[FW] half=floatDiv2(floatOne()); // = 0.5
	bit[FW] th=floatOne(); th[M-2]=1; // = 1.5
	bit[FW] hxx=floatMul(floatMul(half,in),floatMul(t,t));
	hxx[SB]=1; //negate
	return floatMul(t,floatAdd(th,hxx));
}

bit alwaysOne(bit[FW] in)
{
	return 1;
}

bit equiv(bit[FW] in) implements alwaysOne
{
	in[SB]=0;
	in[M-1]=1;
	bit[FW] one=floatOne();
	bit[FW] s=isqrt(in);
	bit[FW] m=floatMul(s,s);
	m=floatMul(m,in);
	m[SB]=1;
	bit[FW] dif=floatAdd(m,one); //computes 1-in*s^2
	bit[FW] ub=floatOne(); ub[SB-2]=1; //=2^4
	bit[FW] lb=ub; lb[SB-1]=0; //=1/ub
	bit[FW] eps=floatDiv4(floatDiv16(floatDiv16(floatOne()))); eps[0]=1;
//	bit[FW] eps=floatDiv2(floatOne()); eps[M-4]=1;
	if(floatAbsLess(lb,s) && floatAbsLess(s,ub) && floatAbsLess(dif,eps))
		return 1;
	else
		return 0;
}
