int L = 3;
int W = 8;  /* FIXME should be an exponent of L. */

/* 
 * Exponentiation.
 */
generator int
exp (int b, int e) /* automatically rewritten */
{
    int a = 1;

    loop (e)
	a = a * b;

    return a;
}

/*
 * Casting from integer to bit-vector (currently not supported by the language).
 */
generator bit[W]
intToBit (int v) /* automatically rewritten */
{
    bit[W] output;

    for (int i = 0; i < W; ++i) {
	output[i] = (v % 2) > 0;
	v = v / 2;
    }

    return output;
}

/*
 * Template for linear combination with single variable.
 */
generator int
tem_linear (int a, int x, int b) /* automatically rewritten */
{
    return (a * x +  b);
}

/*
 * Template for linear combination with two variables.
 */
generator int
tem_linear2 (int a, int x, int b, int y, int c) /* automatically rewritten */
{
    return tem_linear (a, x, 0) + tem_linear (b, y, c);
}

/*
 * Non-deterministic sign inverter.
 */
generator int
any_sign (int a) /* automatically rewritten */
{
    return (?? ? a : -a);
}

/*
 * Shift amount generator, returning an 2^O(i+l).
 */
generator int
some_shift (int i, int l) /* automatically rewritten */
{
    int s;

    s = exp (2, tem_linear2 (__, i, __, l, __));
    return s;
}

/* 
 * Bit-mask generator of the form (0^{n}1^{n})^{2^l/2n}, where n is
 * correlated with the value of the iteration counter, i.
 */
generator bit[W]
some_filter (int i, int l) /* automatically rewritten */
{
    bit[W] m;
    int s;

    s = exp (2, tem_linear (__, l, __));
    m = (bit[W]) (exp (2, tem_linear (__, s, __)) - 1);
    loop (tem_linear2 (__, i, __, l, __)) {
	s = s / 2;
	m = m ^ (m >> s);
    }

    return m;
}

/*
 * Sketch: in place computation in logarithmic number of steps.
 */
int
sk_count (bit[W] a) implements count
{
    bit[W] m;
    int s = 0;

    for (int i = 0; i < tem_linear (__, L, __); i++) {
	s = some_shift (i, L);
	m = some_filter (i, L);
	a = (a & m) + ((a << s) & m);
    }

    return (int) a;
}

/*
 * Spec: linear counting of one-bits.
 */
int
count (bit[W] a)
{
    int ret = 0;

    for (int i = 0; i < W; i++)
	ret = ret + a[i];

    return ret;
}

