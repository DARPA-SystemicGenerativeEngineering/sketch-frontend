include "../primitives.skh";
#include "list.skh"

void add (List L, int key) {
    Node n = newNode (key, null, 0);
    Node next = null, pred = null;

    bit outerMore = 1, innerMore = 1;
    bit wasSwapped = 0;
    for (int i = 0; i < 2; ++i) {
        if (outerMore) {
            next = L.head;
            pred = null;

            innerMore = 1;
            for (int j = 0; j < 5; ++j) {
                if (innerMore) {
                    if (key < next.key) {
                        n.next = next;
                        CAS (wasSwapped, pred.next, next, n);
                        innerMore = 0;
                        outerMore = !wasSwapped;
                    } else {
                        pred = next;
                        next = next.next;
                    }
                }
            }
        }
    }
    assert wasSwapped;
}

int remove (List L, int key) {
    Node next = null, pred = null;

    bit outerMore = 1, innerMore = 1;
    bit wasSwapped = 0;
    for (int i = 0; i < 2; ++i) {
        if (outerMore) {
            next = L.head;
            pred = null;

            innerMore = 1;
            for (int j = 0; j < 5; ++j) {
                if (innerMore) {
                    if (key == next.key && !next.marked) {
                        CAS (wasSwapped, next.marked, 0, 1);
                        if (wasSwapped) {
                            CAS (wasSwapped, pred.next, next, next.next);
                            return next.key;
                        }
                        innerMore = 0;
                        outerMore = !wasSwapped;
                    } else if (key <= next.key) {
                        return -1;
                    } else {
                        pred = next;
                        next = next.next;
                    }
                }
            }
        }
    }
    return -1;
}

bit verify (List L) {
    assertValidList (L);
    assert !find (L, 1);
    assert !find (L, 2);
    return 1;
}

bit verifier () { return 1; }
bit main () implements verifier {
    List L = newList ();
    int[2] popped = 0;

    add (L, 1);
    add (L, 2);
    fork (int i; 2) {
        assert (i+1) == remove (L, i+1);
    }

    return verify (L);
}
