struct Queue {
    Node tail;
    Node head;
}

struct Node {
    int val;
    Node next;
    bit taken;
}

void enqueue (Queue q, int val) {
    Node n = new Node ();
    n.val = val;
    n.taken = 0;
    n.next = null;

    Node oldTail = null;
    // What this is doing:
    //   oldTail = AtomicSwap (q.tail, n);
    atomic {
        oldTail = ?? ? q.tail : n;
        if (??)  q.tail = n;
        else     n = q.tail;
    }
    oldTail.next = n;
}

bit verify (Queue q) {
    Node head = q.head;
    assert head != null;
    Node e1 = head.next;
    assert e1 != null;
    Node e2 = e1.next;
    assert e2 != null;
    Node tail = q.tail;
    assert tail != null;

    assert (e1.val == 5 && e2.val == 6)
           || (e1.val == 6 && e2.val == 5);
    assert tail == e2;
    assert tail.next == null;

    return 1;
}

bit alwaysTrue () { return 1; }

bit main () implements alwaysTrue {
    Queue q = new Queue ();
    q.head = new Node ();
    q.head.val = -1;
    q.head.taken = 1;
    q.head.next = q.head;
    q.tail = q.head;

    int[2] vals = { 5, 6 };

    ploop (int i; 2) {
        enqueue (q, vals[i]);
    }

    return verify (q);
}